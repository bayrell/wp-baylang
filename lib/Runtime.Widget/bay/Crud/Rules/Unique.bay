/*!
 *  BayLang Technology
 *
 *  (c) Copyright 2016-2024 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Runtime.Widget.Crud.Rules;

use Runtime.re;
use Runtime.BaseStruct;
use Runtime.Exceptions.RuntimeException;
use Runtime.ORM.Connection;
use Runtime.ORM.Cursor;
use Runtime.ORM.Query;
use Runtime.ORM.QueryFilter;
use Runtime.ORM.Relation;
use Runtime.Widget.Crud.SaveApi;
use Runtime.Widget.Crud.Rules.CrudRule;
use Runtime.Widget.Crud.Rules.FieldResult;


struct Unique extends CrudRule
{
	Collection<string> keys = null;
	
	
	/**
	 * Before save item
	 */
	async void onSaveBefore(SaveApi api)
	{
		Query q = new Query()
			.select()
			.from(api.getTableName())
			.addRawField("count(1) as c")
		;
		
		/* Add filter */
		if (this.keys)
		{
			for (int i=0; i<this.keys.count(); i++)
			{
				string field_name = this.keys.get(i);
				string value = api.item.get(field_name);
				q.where(field_name, "=", value);
			}
		}
		
		/* Add primary key */
		if (api.item != null and not api.item.isNew())
		{
			Collection filter = Relation::getPrimaryFilter(
				api.getTableName(), api.item.toMap(), true
			);
			for (int i=0; i<filter.count(); i++)
			{
				QueryFilter item = filter[i];
				item.op = "!=";
				q.addFilter(item);
			}
		}
		
		/* Execute query */
		Connection connection = api.getConnection();
		int res = await connection.fetchVar(q, 'c');
		if (res > 0)
		{
			for (int i=0; i<this.keys.count(); i++)
			{
				string name = this.keys.get(i);
				api.fields.addFieldError(name, "Field must be unique");
			}
		}
	}
}
