/*!
 *  BayLang Technology
 *
 *  (c) Copyright 2016-2024 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Runtime.Widget.Form;

use Runtime.BaseObject;
use Runtime.Web.ApiResult;
use Runtime.Widget.Form.FormModel;
use Runtime.Widget.Form.FormStorageInterface;


class FormSaveStorage extends BaseObject implements FormStorageInterface
{
	string api_name = "";
	FormModel form = null;
	
	
	/**
	 * Returns api name
	 */
	string getApiName() => this.api_name;
	
	
	/**
	 * Returns method name
	 */
	string getMethodName(string name)
	{
		if (name == "load") return "actionItem";
		if (name == "submit") return "actionSave";
		return "";
	}
	
	
	/**
	 * Set form
	 */
	void setForm(FormModel form)
	{
		this.form = form;
	}
	
	
	/**
	 * Constructor
	 */
	void constructor(Dict params = null)
	{
		parent();
		this._assign_values(params);
	}
	
	
	/**
	 * Load form
	 */
	async ApiResult load()
	{
		Dict post_data =
		{
			"pk": this.form.pk,
		};
		post_data = this.form.mergePostData(post_data, "load");
		ApiResult res = await this.form.layout.callApi({
			"api_name": this.getApiName(),
			"method_name": this.getMethodName("load"),
			"data": post_data
		});
		return res;
	}
	
	
	/**
	 * Submit form
	 */
	async ApiResult submit()
	{
		Dict post_data =
		{
			"pk": this.form.pk,
			"item": this.form.getPostItem(),
		};
		post_data = this.form.mergePostData(post_data, "submit");
		ApiResult res = await this.form.layout.callApi({
			"api_name": this.getApiName(),
			"method_name": this.getMethodName("submit"),
			"data": post_data
		});
		return res;
	}
}