/*!
 *  BayLang Technology
 *
 *  (c) Copyright 2016-2024 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Runtime.Widget.Dialog;

use Runtime.Callback;
use Runtime.Web.Messages.ClickMessage;
use Runtime.Widget.ButtonModel;
use Runtime.Widget.Dialog.DialogMessage;
use Runtime.Widget.Dialog.DialogModel;
use Runtime.Widget.Dialog.DialogModelException;
use Runtime.Widget.Dialog.PromptDialog;


class PromptDialogModel extends DialogModel
{
	string component = classof PromptDialog;
	string value = "";
	string old_value = "";
	
	
	/**
	 * Init widget settings
	 */
	void initWidget(Dict params)
	{
		parent(params);
		
		/* Setup close buttons */
		this.buttons.addButton({
			"content": "Cancel",
			"widget_name": "cancel_button",
			"styles": [
				"gray"
			],
			"events": {
				"click": new Callback(this, "onCloseButtonClick"),
			},
		});
		
		/* Setup confirm button */
		this.buttons.addButton({
			"content": "Ok",
			"widget_name": "confirm_button",
			"styles": [
				"success"
			],
			"events": {
				"click": new Callback(this, "onConfirmButtonClick"),
			},
		});
		
		/* Buttons style */
		this.buttons.styles.add("align-end");
	}
	
	
	/**
	 * Set value
	 */
	void setValue(string value)
	{
		this.value = value;
	}
	
	
	/**
	 * Add close button click
	 */
	void onCloseButtonClick(ClickMessage message)
	{
		this.hide();
	}
	
	
	/**
	 * Add confirm button click
	 */
	void onConfirmButtonClick(ClickMessage message)
	{
		try
		{
			this.emit(new DialogMessage{
				"name": "confirm",
				"value": this.value,
			});
		}
		catch (DialogModelException e)
		{
			this.result.setException(e);
			return;
		}
		this.hide();
	}
}