/*!
 *  BayLang Technology
 *
 *  (c) Copyright 2016-2024 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace BayLang.Constructor.Backend.Api;

use Runtime.fs;
use Runtime.Serializer;
use Runtime.SerializerBase64;
use Runtime.Exceptions.ApiError;
use Runtime.Exceptions.ItemNotFound;
use Runtime.Exceptions.RuntimeException;
use Runtime.Web.BaseApi;
use Runtime.Web.Annotations.ApiMethod;
use BayLang.Constructor.Backend.ApiHook;
use BayLang.Helper.Module;
use BayLang.Helper.Project;
use BayLang.Helper.Widget;
use BayLang.LangBay.TranslatorBay;
use BayLang.OpCodes.BaseOpCode;
use BayLang.OpCodes.OpModule;


class WidgetApi extends BaseApi
{
	/**
	 * Returns api name
	 */
	static string getApiName() => "baylang.constructor.widget";
	
	
	/**
	 * Load widget
	 */
	@ApiMethod
	async static void getOpCode(BaseApi api)
	{
		string project_id = api.post_data.get("project_id");
		string current_widget = api.post_data.get("current_widget");
		
		/* Find project */
		Project project = await ApiHook::getProject(project_id);
		if (not project)
		{
			throw new ApiError( new ItemNotFound(project_id, "Project") );
		}
		
		/* Load project */
		await project.load();
		
		/* Find widget */
		Widget widget = await project.getWidget(current_widget);
		if (not widget)
		{
			throw new ApiError( new ItemNotFound(current_widget, "Widget") );
		}
		
		/* Load widget */
		await widget.load();
		
		/* Get widget op code */
		BaseOpCode component = await widget.getComponentOpCode();
		BaseOpCode model = await widget.getModelOpCode();
		
		/* Get component path */
		if (not component)
		{
			throw new ApiError(
				new RuntimeException("Failed to load widget '" ~ current_widget ~ "'")
			);
		}
		
		/* Success */
		api.success({
			"data": {
				"component": component,
				"model": model,
			},
		});
	}
	
	
	/**
	 * Save widget
	 */
	@ApiMethod
	async static void save(BaseApi api)
	{
		string project_id = api.post_data.get("project_id");
		string current_widget = api.post_data.get("current_widget");
		
		/* Find project */
		Project project = await ApiHook::getProject(project_id);
		if (not project)
		{
			throw new ApiError( new ItemNotFound(project_id, "Project") );
		}
		
		/* Load project */
		await project.load();
		
		/* Find widget */
		Widget widget = await project.getWidget(current_widget);
		if (not widget)
		{
			throw new ApiError( new ItemNotFound(current_widget, "Widget") );
		}
		
		/* Load widget */
		await widget.load();
		
		/* Get component path */
		string component_path = widget.getComponentPath();
		if (not component_path)
		{
			throw new ApiError(
				new RuntimeException("Failed to load widget '" ~ current_widget ~ "'")
			);
		}
		
		/* Save file */
		static::saveFile(widget, widget.getComponentPath(), api.post_data.get("component"));
		static::saveFile(widget, widget.getModelPath(), api.post_data.get("model"));
		
		/* Update assets */
		widget.module.updateAssets();
		ApiHook::updateAssets();
		
		/* Success */
		api.success();
	}
	
	
	/**
	 * Save file
	 */
	static void saveFile(Widget widget, string file_path, string content)
	{
		/* Get op_code */
		Serializer serializer = new SerializerBase64();
		serializer.setFlag(Serializer::ALLOW_OBJECTS);
		OpModule op_code = serializer.decode(content);
		
		/* Translate */
		TranslatorBay translator = new TranslatorBay();
		list res = translator::translate(translator, op_code);
		string content = res.get(1);
		
		/* Save content */
		await fs::saveFile(file_path, content);
		
		/* Compile file */
		string relative_src_file_path = widget.module.getRelativeSourcePath(file_path);
		widget.module.compile(relative_src_file_path);
		
		/* widget.module.translateLanguages(relative_src_file_path, op_code); */
	}
}