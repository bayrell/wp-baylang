/*!
 *  BayLang Technology
 *
 *  (c) Copyright 2016-2024 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace BayLang.Constructor.Frontend.Widget;

use Runtime.Callback;
use Runtime.Entity.Factory;
use Runtime.Web.BasePageModel;
use Runtime.Web.ModelFactory;
use Runtime.Web.RenderContainer;
use Runtime.Web.Messages.ClickMessage;
use Runtime.Widget.Form.FormDeleteStorage;
use Runtime.Widget.Form.FormMessage;
use Runtime.Widget.Form.FormModel;
use Runtime.Widget.Form.FormSubmitStorage;
use Runtime.Widget.Table.TableDialogModel;
use Runtime.Widget.Table.TableMessage;
use Runtime.Widget.ButtonModel;
use Runtime.Widget.Label;
use Runtime.Widget.RowButtonsModel;
use Runtime.Widget.SelectModel;
use BayLang.Constructor.Frontend.Widget.WidgetList;


class WidgetListModel extends BasePageModel
{
	string component = classof WidgetList;
	string project_id = "";
	string module_id = "";
	SelectModel modules = null;
	TableDialogModel table = null;
	RowButtonsModel top_buttons = null;
	
	
	/**
	 * Init widget settings
	 */
	void initWidget(Dict params)
	{
		parent(params);
		
		/* Set route matches */
		this.project_id = this.layout.route.matches.get("project_id");
		
		/* Modules */
		this.modules = this.addWidget(
			classof SelectModel,
			{
				"storage": new Factory
				(
					"Runtime.Widget.Table.TableStorage",
					{
						"api_name": "baylang.constructor.module::search",
					}
				),
				"widget_name": "modules",
				"transform": Map (Dict item) => {
					"key": item.get("id"),
					"value": item.get("id"),
				},
				"foreign_key":
				{
					"project_id": this.project_id,
				},
			}
		);
		
		/* Add table */
		this.table = this.addWidget(
			classof TableDialogModel,
			{
				"widget_name": "table",
				"get_title": void (Dict params)
				{
					string action = params.get("action");
					Dict item = params.get("item");
					if (action == "add")
					{
						return "Add widget";
					}
					if (action == "edit")
					{
						return "Edit widget " ~ item.get("id");
					}
					if (action == "delete")
					{
						return "Delete widget " ~ item.get("id");
					}
					return "";
				},
				"styles": ["border"],
				
				/* Table storage */
				"storage": new Factory
				(
					"Runtime.Widget.Table.TableStorage",
					{
						"api_name": "baylang.constructor.widget::search",
					}
				),
				
				/* Add form */
				"add_form": new ModelFactory(
					classof FormModel,
					{
						"widget_name": "add_form",
						"primary_key": ["id"],
						"foreign_key":
						{
							"project_id": this.project_id,
						},
						"storage": new Factory(
							classof FormSubmitStorage,
							{
								"api_name": "baylang.constructor.widget::save"
							}
						),
						"fields":
						[
							{
								"name": "module_id",
								"label": "Modules",
								"model": this.modules,
							},
							{
								"name": "id",
								"label": "Name",
								"component": "Runtime.Widget.Input",
								"default": "WidgetModel",
							}
						],
						"events":
						{
							"field_change": new Callback(this, "onFormFieldChange"),
						},
					}
				),
				
				/* Delete form */
				"delete_form": new ModelFactory(
					classof FormModel,
					{
						"widget_name": "delete_form",
						"primary_key": ["id"],
						"foreign_key":
						{
							"project_id": this.project_id,
						},
						"storage": new Factory(
							classof FormDeleteStorage,
							{
								"api_name": "baylang.constructor.widget::save"
							}
						),
					}
				),
				
				/* Foreign key */
				"foreign_key": {
					"project_id": this.project_id,
					"module_id": this.module_id,
				},
				
				/* Table fields */
				"fields":
				[
					{
						"name": "row_number",
					},
					{
						"name": "id",
						"label": "Widget name",
						"component": "Runtime.Widget.Label",
					},
					{
						"name": "row_buttons",
						"model": new ModelFactory(
							"Runtime.Widget.Table.TableRowButtonsModel",
							{
								"edit": false,
								"buttons":
								[
									{
										"widget_name": "edit_button",
										"dest": "delete_button",
										"kind": "before",
										"href": void (Dict data)
										{
											Dict item = data.get("item");
											return this.layout.url(
												"baylang:project:widget:edit",
												{
													"project_id": this.project_id,
													"module_id": this.module_id,
													"widget_name": item.get("id"),
												}
											);
										},
										"content": "Edit",
										"styles":
										[
											"default",
											"small"
										],
									}
								],
								
								/* Events */
								"events":
								{
									"click": new Callback(this, "onRowButtonClick"),
								},
							}
						),
					},
				],
			}
		);
		
		/* Add top buttons */
		this.top_buttons = this.addWidget(
			classof RowButtonsModel,
			{
				"widget_name": "top_buttons",
				"buttons":
				[
					new ModelFactory
					(
						"Runtime.Widget.Table.AddButtonModel",
						{
							"table": this.table,
						}
					)
				],
			}
		);
	}
	
	
	/**
	 * Build title
	 */
	void buildTitle(RenderContainer container)
	{
		this.layout.setPageTitle("Widgets");
	}
	
	
	/**
	 * Table row button click
	 */
	void onRowButtonClick(ClickMessage message)
	{
		ButtonModel button = message.widget;
		if (button.widget_name != "delete_button") return;
		
		/* Set delete form foreign key */
		string module_id = message.data.get("item").get("module_id");
		this.table.delete_form.foreign_key.set("module_id", module_id);
	}
	
	
	/**
	 * Form field change
	 */
	void onFormFieldChange(FormMessage message)
	{
		string field_name = message.field_name;
		string old_value = message.old_value;
		string value = message.value;
		if (field_name != "module_id") return;
		
		/* Get form */
		FormModel form = message.widget;
		string widget_id = form.item.get("id");
		
		/* Set foreign key */
		form.foreign_key.set("module_id", value);
		
		/* Set widget name */
		if (rs::indexOf(widget_id, old_value) == 0)
		{
			widget_id = rs::substr(widget_id, rs::strlen(old_value));
		}
		if (value != "" and rs::indexOf(widget_id, value) == 0)
		{
			return;
		}
		if (rs::substr(widget_id, 0, 1) == ".")
		{
			widget_id = rs::substr(widget_id, 1);
		}
		if (rs::substr(widget_id, 0, 11) == "Components.")
		{
			widget_id = rs::substr(widget_id, 11);
		}
		if (value != "")
		{
			widget_id = value ~ ".Components." ~ widget_id;
		}
		form.item.set("id", widget_id);
	}
}