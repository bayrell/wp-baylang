/*!
 *  BayLang Technology
 *
 *  (c) Copyright 2016-2024 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace BayLang.Constructor.Frontend.Editor.Parameters;

use Runtime.lib;
use Runtime.Web.BaseModel;
use BayLang.Constructor.Frontend.Editor.Parameters.ParameterModel;
use BayLang.Constructor.Frontend.Editor.Processor.CodeProcessor;
use BayLang.OpCodes.OpDict;
use BayLang.OpCodes.OpDictPair;
use BayLang.OpCodes.OpNew;


class ParameterFactoryModel extends ParameterModel
{
	string path = "";
	
	
	/**
	 * Find OpDictPair by path
	 */
	OpDictPair getCode()
	{
		OpNew op_code = this.op_code.value;
		if (not (op_code instanceof OpNew)) return;
		if (op_code.args.count() != 2) return;
		
		OpDict op_code_params = op_code.args.get(1);
		if (not (op_code_params instanceof OpDict)) return;
		
		OpDictPair op_code_api_name = op_code_params.values.findItem(lib::equalAttr("api_name"));
		if (not op_code_api_name) return;
		
		return op_code_api_name;
	}
	
	
	/**
	 * Set op_code
	 */
	void setOpCode(OpDictPair op_dict_pair)
	{
		this.op_code = op_dict_pair;
		OpDictPair code = this.getCode();
		if (code)
		{
			this.value = CodeProcessor::getValueFromOpCode(code.value);
		}
	}
	
	
	/**
	 * Set value
	 */
	void setValue(var value)
	{
		this.value = value;
		OpDictPair code = this.getCode();
		if (code)
		{
			code.value = CodeProcessor::getOpCodeByValue(value);
		}
	}
	
	
	/**
	 * Update model value
	 */
	void updateModelValue(BaseModel model)
	{
		var name = this.name;
		var path = this.path;
		var value = this.value;
		#ifcode JAVASCRIPT then
		model[name][path] = value;
		#endif
	}
}