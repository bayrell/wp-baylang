/*!
 *  BayLang Technology
 *
 *  (c) Copyright 2016-2024 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace BayLang.Constructor.Frontend.Editor.Parameters;

use Runtime.lib;
use Runtime.Web.BaseModel;
use BayLang.Constructor.Frontend.Editor.Parameters.Parameter;
use BayLang.Constructor.Frontend.Editor.Processor.CodeProcessor;
use BayLang.OpCodes.BaseOpCode;
use BayLang.OpCodes.OpDictPair;
use BayLang.OpCodes.OpHtmlAttribute;
use BayLang.OpCodes.OpHtmlContent;
use BayLang.OpCodes.OpHtmlValue;
use BayLang.OpCodes.OpString;


class ParameterRawHtml extends Parameter
{
	string name = "is_raw_html";
	string label = "Is Raw HTML";
	string component = "Runtime.Widget.Select";
	string value = "0";
	Dict props = {
		"show_select_value": false,
		"options": [
			{ "key": "0", "value": "No" },
			{ "key": "1", "value": "Yes" },
		],
	};
	
	
	/**
	 * Init parameter
	 */
	public void init()
	{
		BaseOpCode text_item = this.widget.code.items.items.get(0);
		if (text_item instanceof OpHtmlValue)
		{
			if (text_item.kind == "raw")
			{
				this.value = "1";
			}
		}
	}
	
	
	/**
	 * Returns HTML content
	 */
	string getHtmlContent()
	{
		BaseOpCode text_item = this.widget.code.items.items.get(0);
		if (text_item instanceof OpHtmlContent)
		{
			return text_item.value;
		}
		else if (text_item instanceof OpHtmlValue)
		{
			if (text_item.value instanceof OpString)
			{
				return text_item.value.value;
			}
		}
		return "";
	}
	
	
	/**
	 * Set value
	 */
	void setValue(var value)
	{
		this.value = value;
		
		/* Update key debug attr */
		OpHtmlAttribute attr_item = this.widget.code.attrs.findItem(
			lib::equalAttr("key", "@key_debug")
		);
		if (attr_item)
		{
			if (this.value == "1")
			{
				if (rs::indexOf(attr_item.value.value, "_raw") == -1)
				{
					attr_item.value.value ~= "_raw";
				}
			}
			else
			{
				if (rs::indexOf(attr_item.value.value, "_raw") != -1)
				{
					attr_item.value.value = rs::replace("_raw", "", attr_item.value.value);
				}
			}
		}
		
		/* Set content */
		BaseOpCode code = null;
		if (this.value == "1")
		{
			code = new OpHtmlValue
			{
				"kind": "raw"
				"value": new OpString
				{
					"value": this.getHtmlContent(),
				},
			};
		}
		else
		{
			code = new OpHtmlContent
			{
				"value": this.getHtmlContent(),
			};
		}
		
		/* Add content */
		this.widget.code.items.items = [];
		this.widget.code.items.items.push(code);
	}
}