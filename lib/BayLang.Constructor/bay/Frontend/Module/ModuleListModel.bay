/*!
 *  BayLang Technology
 *
 *  (c) Copyright 2016-2024 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace BayLang.Constructor.Frontend.Module;

use Runtime.Callback;
use Runtime.Entity.Factory;
use Runtime.Web.BasePageModel;
use Runtime.Web.ModelFactory;
use Runtime.Web.RenderContainer;
use Runtime.Web.Messages.ClickMessage;
use Runtime.Widget.Table.TableModel;
use Runtime.Widget.Label;
use Runtime.Widget.RowButtonsModel;
use BayLang.Constructor.Frontend.Module.ModuleList;


class ModuleListModel extends BasePageModel
{
	string component = classof ModuleList;
	string project_id = "";
	
	
	/**
	 * Init widget settings
	 */
	void initWidget(Dict params)
	{
		parent(params);
		
		/* Project id */
		this.project_id = this.layout.route.matches.get("project_id");
		
		/* Add table */
		this.table = this.addWidget(
			classof TableModel,
			{
				"widget_name": "table",
				"get_title": void (Dict params)
				{
					string action = params.get("action");
					Dict item = params.get("item");
					if (action == "add")
					{
						return "Add module";
					}
					if (action == "edit")
					{
						return "Edit module " ~ item.get("id");
					}
					if (action == "delete")
					{
						return "Delete module " ~ item.get("id");
					}
					return "";
				},
				"styles": ["border"],
				
				/* Table storage */
				"storage": new Factory
				(
					"Runtime.Widget.Table.TableStorage",
					{
						"api_name": "baylang.constructor.module::search",
					}
				),
				
				/* Foreign key */
				"foreign_key": {
					"project_id": this.project_id,
				},
				
				/* Table fields */
				"fields":
				[
					{
						"name": "row_number",
					},
					{
						"name": "id",
						"label": "ID",
						"component": "Runtime.Widget.Label",
					},
					{
						"name": "row_buttons",
						"model": new ModelFactory(
							"Runtime.Widget.RowButtonsModel",
							{
								"buttons":
								[
									{
										"widget_name": "open",
										"content": "Open",
										"href": void (Dict data)
										{
											Dict item = data.get("item");
											return this.layout.url(
												"baylang:project:widgets",
												{
													"project_id": this.project_id,
													"module_id": item.get("id"),
												}
											);
										},
										"styles":
										[
											"default",
											"small"
										],
									}
								],
							}
						),
					},
				],
			}
		);
	}
	
	
	/**
	 * Build title
	 */
	void buildTitle(RenderContainer container)
	{
		this.layout.setPageTitle("Modules");
	}
}