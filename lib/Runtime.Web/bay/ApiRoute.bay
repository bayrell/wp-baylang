/*!
 *  BayLang Technology
 *
 *  (c) Copyright 2016-2024 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Runtime.Web;

use Runtime.Exceptions.AbstractException;
use Runtime.Web.ApiProvider;
use Runtime.Web.ApiResult;
use Runtime.Web.BaseRoute;
use Runtime.Web.Bus;
use Runtime.Web.BusLocal;
use Runtime.Web.Cookie;
use Runtime.Web.JsonResponse;
use Runtime.Web.RenderContainer;
use Runtime.Web.RouteAction;
use Runtime.Web.RouteInfo;


class ApiRoute extends BaseRoute
{
	/**
	 * Returns routes
	 */
	static Collection<RouteInfo> getRoutes()
	{
		return
		[
			new RouteAction
			{
				"uri": "/api/{service}/{api_name}/{method_name}/",
				"name": "runtime:web:api",
				"action": "actionIndex",
				"pos": 1000,
			},
		];
	}
	
	
	/**
	 * Action index
	 */
	async static void actionIndex(RenderContainer container)
	{
		/* Call api */
		string service = container.route.matches["service"];
		string api_name = container.route.matches["api_name"];
		string method_name = container.route.matches["method_name"];
		
		ApiResult api_result = null;
		BusLocal bus = @.provider(classof BusLocal);
		try
		{
			api_result = await bus.send({
				"service": service,
				"api_name": api_name,
				"method_name": method_name,
				"data": container.request.payload.get("data"),
				"backend_storage": container.layout.backend_storage,
				"container": container,
			});
		}
		catch (AbstractException e)
		{
			api_result = new ApiResult();
			api_result.exception(e);
		}
		
		/* Create response */
		container.setResponse( new JsonResponse(api_result.getContent()) );
		
		/* Set cookie */
		api_result.cookies.each(
			void (Cookie cookie) use (container)
			{
				container.addCookie(cookie);
			}
		);
		
		/* HTTP error if exception */
		/*if (api.isException())
		{
			container.response.http_code = 500;
		}*/
	}
}